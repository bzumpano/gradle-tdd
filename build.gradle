buildscript {
    ext {
        springBootVersion = '1.3.3.RELEASE'
        flywayVersion = '3.2.1'
    }
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}"
        classpath "org.flywaydb:flyway-gradle-plugin:${flywayVersion}"
    }
}

apply plugin: 'java'
apply plugin: 'pmd'
apply plugin: 'eclipse-wtp'
apply plugin: 'spring-boot'
apply plugin: 'war'
apply plugin: 'org.flywaydb.flyway'


// TODO Lembrar de configurar "excludeDevtools=true" para profile=prod
war {
    baseName = 'gradle-tdd'
    version = '0.0.1-SNAPSHOT'
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    mavenCentral()
}

configurations {
    providedRuntime
}

dependencies {

//  compile('org.springframework.boot:spring-boot-starter-security')
    compile('org.springframework.boot:spring-boot-starter-thymeleaf')
    compile('org.springframework.boot:spring-boot-starter-data-jpa')
    compile('org.springframework.boot:spring-boot-starter-web')
    compile("org.springframework.boot:spring-boot-devtools")
    compile('org.flywaydb:flyway-core')

    runtime('com.h2database:h2')

    providedRuntime('org.springframework.boot:spring-boot-starter-tomcat')

    testCompile('org.springframework.boot:spring-boot-starter-test')
}

test {
    systemProperty "spring.profiles.active", "test"
}

ext {
    // default: development. Override via command line arg:
    // ./gradlew myTask -Dprofile=[dev|test|demo|prod]
    profile = System.getProperty("profile", "dev")
}


task wrapper(type: Wrapper) {
    gradleVersion = '2.11'
}

bootRun {
    // SpringBootDevTools
    addResources = true

    systemProperty "spring.profiles.active", profile
}

pmd {
    consoleOutput = true
    ruleSets = [
                "java-basic",
                "java-braces",
                "java-strings",
                "java-clone",
                "java-imports",
                "java-migrating",
                "java-j2ee",
//                "java-coupling",
                "java-junit",
                "java-optimizations",
                "java-codesize",
//                "java-design",
                "java-sunsecure",
                "java-unusedcode"
                ]
}

flyway {

    Properties props = new Properties()
    props.load(new FileInputStream("${projectDir}/src/main/resources/flyway.properties"))

    def prefix = "flyway.${profile}"

    url = props.getProperty("${prefix}.url")
    user = props.getProperty("${prefix}.user")
    password = props.getProperty("${prefix}.password")
    schemas = props.getProperty("${prefix}.schemas")
    locations = Arrays.asList(props.getProperty("${prefix}.locations").split(","))
}

task flywayLog << {
    println "    db: ${flyway.user}@${flyway.url} - schema: ${flyway.schemas}"
}

flywayMigrate.dependsOn(flywayLog)
flywayInfo.dependsOn(flywayLog)
flywayRepair.dependsOn(flywayLog)
flywayClean.dependsOn(flywayLog)
flywayValidate.dependsOn(flywayLog)
flywayInit.dependsOn(flywayLog)
